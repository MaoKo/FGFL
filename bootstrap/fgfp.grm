/* LR(1) Grammar  */

/*

<FGFP>		->	<FGFP> <Prod> | <Prod> ;
<Prod>		->	NON_TER ARROW <Rhs> SEMI ;
<Rhs>		->	<Rhs> UNION <OptList> | <OptList> ;
<OptList>	->	<List> | // epsilon  ;
<List>		->	<List> <Atom> | <Atom> ;
<Atom>		->	NON_TER | TOKEN ;

*/

/* Equivalent LL(1) */

<Syntax>	->	<Inst> <Syntax'> ;
<Syntax'>	->	<Inst> <Syntax'> | /* epsilon */ ;
<Inst>		->	<Prod> | <Directive> ;
<Directive>	->	START NON_TER SEMI ;
<Prod>		->	NON_TER ARROW <Rhs> SEMI ;
<Rhs>		->	<OptList> <Rhs'> ;
<Rhs'>		->	UNION <OptList> <Rhs'> | /* epsilon */ ;
<OptList>	->	<List> | /* epsilon */ ;
<List>		->	<Atom> <List'> ;
<List'>		->	<Atom> <List'> | /* epsilon */ ;
<Atom>		->	NON_TER | TOKEN ;


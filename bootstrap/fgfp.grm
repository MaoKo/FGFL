[extern] 'fgfx.lex.h' ;
[start] <syntax> ;

<syntax> -> <inst> <syntax'> ;
<syntax'> -> <inst> <syntax'> | %empty% ;

<inst> -> <prod> | <directive> ;

<directive> -> <startdir> | <tokendir> | <aliasdir> ;

<startdir> -> '[start]' NON_TER ';' ;

<tokendir> -> '[extern]' LITERAL ';' ;

<aliasdir> -> '[alias]' <aliaslist> ';' ;

<aliaslist> -> GLOBAL_TOK '->' LITERAL <aliaslist'> ;
<aliaslist'> -> ',' GLOBAL_TOK '->' LITERAL <aliaslist'> | %empty% ;

<prod> -> NON_TER '->' <rhs> ';' ;

<rhs> -> <optlist> <rhs'> ;

<rhs'> -> '|' <optlist> <rhs'> | %empty% ;

<optlist> -> <list> | %empty% ;

<list> -> <atom> <list'> ;
<list'> -> <atom> <list'> | %empty% ;

<atom> -> NON_TER | GLOBAL_TOK | LITERAL ;

[alias]
	SEMI	-> ';'		, COMMA	-> ','		,
	ARROW	-> '->'		, START	-> '[start]'	,
	EXTERN	-> '[extern]'	, ALIAS	-> '[alias]'	,
	UNION	-> '|'		;


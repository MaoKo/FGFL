%token "fgfx.lex.h" ;

%alias	SEMI	-> ';' ,
	COMMA	-> ',' ,
	UNION	-> '|' ;

<Syntax>	->	<Inst> <Syntax'>
		;

<Syntax'>	->	<Inst> <Syntax'>
		|	/* epsilon */
		;

<Inst>		->	<Prod>
		|	<Directive>
		;

<Directive>	->	<StartDir>
		|	<TokenDir>
		|	<AliasDir>
		;

<StartDir>	->	START NON_TER ';'
		;

<TokenDir>	->	TOKEN STR ';'
		;

<AliasDir>	->	ALIAS <AliasList> ';'
		;

<AliasList>	->	GLOBAL_TOK ARROW LITERAL <AliasList'>
		;

<AliasList'>	->	',' GLOBAL_TOK ARROW LITERAL <AliasList'>
		|	/* epsilon */
		;

<Prod>		->	NON_TER ARROW <Rhs> ';'
		;

<Rhs>		->	<OptList> <Rhs'>
		;

<Rhs'>		->	'|' <OptList> <Rhs'>
		|	/* epsilon */
		;

<OptList>	->	<List>
		|	/* epsilon */
		;

<List>		->	<Atom> <List'>
		;

<List'>		->	<Atom> <List'>
		|	/* epsilon */
		;

<Atom>		->	NON_TER
		|	GLOBAL_TOK
		|	LITERAL
		;

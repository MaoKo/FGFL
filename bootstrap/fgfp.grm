<FGFP>		->	<FGFP> <Prod> | <Prod> ;
<Prod>		->	NON_TER ARROW <Lhs> SEMI ;
<Lhs>		->	<Lhs> UNION <OptList> | <OptList> ;
<OptList>	->	<List> | /* epsilon */ ;
<List>		->	<List> <Atom> | <Atom> ;
<Atom>		->	NON_TER | TOKEN ;

/* Equivalent LL(1) */

/*

<Syntax>	->	<Prod> <Syntax'> ;
<Syntax'>	->	<Prod> <Syntax'> | ;

<Prod>		->	NON_TER ARROW <Lhs> SEMI ;

<Lhs>		->	<OptList> <Lhs'> ;
<Lhs'>		->	UNION <OptList> <Lhs'> | ;

<OptList>	->	<List> | ;
<List>		->	<Atom> <List'> ;
<List'>		->	<Atom> <List'> | ;

<Atom>		->	NON_TER | TOKEN ;

*/

// FIRST(Atom) = { NON_TER, TOKEN }
// FIRST(List) = { NON_TER, TOKEN }
// FIRST(List') = { Epsilon, NON_TER, TOKEN }
// FIRST(OptList) = { NON_TER, TOKEN, Epsilon }
// FIRST(Lhs') = { UNION, Epsilon }
// FIRST(Lhs) = { NON_TER, TOKEN, Epsilon }
// FIRST(Prod) = { NON_TER }
// FIRST(Syntax') = { NON_TER, Epsilon }
// FIRST(Syntax) = { NON_TER }

// FOLLOW(<Lhs>) = { SEMI }
// FOLLOW(<Lhs'>) = { SEMI }
// FOLLOW(<OptList>) = { UNION, SEMI }
// FOLLOW(<List>) = { UNION, SEMI }
// FOLLOW(<List'>) = { UNION, SEMI }

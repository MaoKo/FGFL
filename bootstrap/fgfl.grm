$EXTERN
{
    'fgfx.lex.h',
};

$PRODUCTION
{
	<FGFL>				->	{ <section> } => ( $RIGHT ) ;
	<section>			->	<skip_section>
						|	<token_section>
						|	<keyword_section>
						|	<state_section>
						;

	<skip_section>		->	'$SKIP'		'{' <regex_list>		'}' ';' ;
	<token_section>		->	'$TOKEN'	'{' <regex_list>		'}' ';' ;
	<keyword_section>	->	'$KEYWORD'	'{' <opt_token_list>	'}' ';' ;
	<state_section>		->	'$STATE'	'{' <opt_state_list>	'}' ';' ;

	<regex_list>		->	{ <state_prefix> TERMINAL <regex_assign> ';' }
								=> ( $RIGHT ) ;

	<state_prefix>		->	'(' <state_prefix_item> ')' | $EMPTY ;
	<state_prefix_item>	->	<token_list> | '*' ;

	<regex_assign>		->	'=' REGEX [ '->' '{' <regex_property> '}' ]
								[ ',' <change_state> ] ;

	<regex_property>	->	'$IGCASE' [ ',' '$FRAG'   ]
						|	'$FRAG'   [ ',' '$IGCASE' ]
						;

	<token_list>		->	TERMINAL [ ',' <opt_token_list> ] ;
	<opt_token_list>	->	TERMINAL [ ',' <opt_token_list> ] | $EMPTY ;

	<opt_state_list>	->	TERMINAL [ '=>' '$INITIAL' ]
								[ ',' <opt_state_list> ] | $EMPTY ;

	<change_state>		->	'(' '$BEGIN' <change_list> ')' ;

	<change_list>		->	<change_state_atom> [ ',' <opt_change_list> ] ;
	<opt_change_list>	->	<change_state_atom> [ ',' <opt_change_list> ]
						|	$EMPTY
						;

	<change_state_atom>	->	TERMINAL | '$NONE' | '$ALL' ;

	<FGFL> = $START;
};

$ALIAS
{
	(SKIP		=> '$SKIP')		,
	(TOKEN		=> '$TOKEN')	,
	(KEYWORD	=> '$KEYWORD')	,
	(IGCASE		=> '$IGCASE')	,
	(STATE		=> '$STATE')	,
	(BEGIN		=> '$BEGIN')	,
	(FRAG		=> '$FRAG')		,
	(INITIAL	=> '$INITIAL')	,
	(ALL		=> '$ALL')		,
	(NONE		=> '$NONE')		,

	(EQUAL		=> '=')			,

	(LBRACE		=> '{')			,
	(RBRACE		=> '}')			,

	(LPAREN		=> '(')			,
	(RPAREN		=> ')')			,

	(SEMI		=> ';')			,
	(COMMA		=> ',')			,

	(ARROW		=> '->')		,
	(BARROW		=> '=>')		,

	(STAR		=> '*')			,
};

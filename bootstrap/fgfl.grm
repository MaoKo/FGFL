$EXTERN
{
    'fgfx.lex.h',
};

$PRODUCTION
{
    <FGFL>              ->  { <section> } => ( $RIGHT ) ;
    <section>           ->  <skip_section>
                        |   <token_section>
                        |   <keyword_section>
                        |   <state_section>
                        ;

    <skip_section>      ->  '$SKIP'     '{' <regex_list>        '}' ';' ;
    <token_section>     ->  '$TOKEN'    '{' <regex_list>        '}' ';' ;
    <keyword_section>   ->  '$KEYWORD'  '{' <opt_token_list>    '}' ';' ;
    <state_section>     ->  '$STATE'    '{' <opt_state_list>    '}' ';' ;

    <regex_list>        ->  { <state_prefix> TERMINAL <regex_assign> ';' }
                                => ( $RIGHT ) ;

    <state_prefix>      ->  '(' <state_prefix_item> ')' | $EMPTY ;
    <state_prefix_item> ->  <token_list> | '*' ;

    <regex_assign>      ->  '=' REGEX [ '->' '$FRAGMENT' ]
                                [ ',' <action_state> ] ;

    <token_list>        ->  TERMINAL [ ',' <opt_token_list> ] ;
    <opt_token_list>    ->  TERMINAL [ ',' <opt_token_list> ] | $EMPTY ;

    <opt_state_list>    ->  TERMINAL [ '=>' '$INITIAL' ]
                                [ ',' <opt_state_list> ] | $EMPTY ;

    <action_state>      ->  '(' <action_pair> ')' ;

    <action_pair>       ->  '$BEGIN' <action_list>
                        |   '$PUSH'  <action_list>
                        |   '$POP'
                        ;

    <action_list>       ->  <change_state_atom> [ ',' <opt_action_list> ] ;
    <opt_action_list>   ->  <change_state_atom> [ ',' <opt_action_list> ]
                        |   $EMPTY
                        ;

    <change_state_atom> ->  '$STAY' | TERMINAL | '*'
                        ;

    <FGFL> = $START;
};

$ALIAS
{
    (SKIP       => '$SKIP')     ,
    (TOKEN      => '$TOKEN')    ,
    (KEYWORD    => '$KEYWORD')  ,
    (STATE      => '$STATE')    ,
    (BEGIN      => '$BEGIN')    ,
    (FRAGMENT   => '$FRAGMENT') ,
    (INITIAL    => '$INITIAL')  ,
    (STAY       => '$STAY')     ,
    (PUSH       => '$PUSH')     ,
    (POP        => '$POP')      ,

    (EQUAL      => '=')         ,

    (LBRACE     => '{')         ,
    (RBRACE     => '}')         ,

    (LPAREN     => '(')         ,
    (RPAREN     => ')')         ,

    (SEMI       => ';')         ,
    (COMMA      => ',')         ,

    (ARROW      => '->')        ,
    (BARROW     => '=>')        ,

    (STAR       => '*')         ,
};
